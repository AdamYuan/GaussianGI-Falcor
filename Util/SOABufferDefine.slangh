#ifdef SOA_BUFFER_DEF

#ifndef SOA_WORD_TYPE
#define SOA_WORD_TYPE float
#endif

#ifndef SOA_WORDS_PER_UNIT
#define SOA_WORDS_PER_UNIT 4
#endif

#ifndef SOA_WORDS_PER_ELEM
#define SOA_WORDS_PER_ELEM 11
#endif

#ifndef SOA_TUPLE_NAME
#define SOA_TUPLE_NAME SOATuple
#endif

#ifndef SOA_IELEMENT_NAME
#define SOA_IELEMENT_NAME ISOAElement
#endif

// Checks
#if !(SOA_WORDS_PER_UNIT == 1 || SOA_WORDS_PER_UNIT == 2 || SOA_WORDS_PER_UNIT == 4)
#error SOA_WORDS_PER_UNIT should be 1 or 2 or 4
#endif

#if !(SOA_WORDS_PER_ELEM > 0)
#error SOA_WORDS_PER_ELEM should be greater than 0
#endif

// Values
#define SOA_UNITS_PER_ELEM (SOA_WORDS_PER_ELEM / SOA_WORDS_PER_UNIT)
#define SOA_WORDS_PER_EXT (SOA_WORDS_PER_ELEM % SOA_WORDS_PER_UNIT)

#define SOA_HAS_UNITS (SOA_UNITS_PER_ELEM > 0)
#define SOA_HAS_EXT (SOA_WORDS_PER_EXT > 0)

#if SOA_HAS_UNITS
#define SOA_UNIT_TYPE vector<SOA_WORD_TYPE, SOA_WORDS_PER_UNIT>
#endif

#if SOA_HAS_EXT
#define SOA_IS_EXT_PADDED ((SOA_WORDS_PER_EXT) == 3)
#define SOA_PADDED_WORDS_PER_EXT (SOA_IS_EXT_PADDED ? 4 : SOA_WORDS_PER_EXT)
#define SOA_EXT_TYPE vector<SOA_WORD_TYPE, SOA_WORDS_PER_EXT>
#define SOA_PADDED_EXT_TYPE vector<SOA_WORD_TYPE, SOA_PADDED_WORDS_PER_EXT>
#if SOA_WORDS_PER_EXT == 1
#define SOA_TEXTURE_PADDED_EXT_TYPE SOA_WORD_TYPE
#else
#define SOA_TEXTURE_PADDED_EXT_TYPE SOA_PADDED_EXT_TYPE
#endif
#endif

public interface SOA_IELEMENT_NAME
{
    SOA_TUPLE_NAME toSOA();
    static This fromSOA(SOA_TUPLE_NAME);
}

public struct SOA_TUPLE_NAME : SOA_IELEMENT_NAME
{
#if SOA_HAS_UNITS
    public SOA_UNIT_TYPE units[SOA_UNITS_PER_ELEM];
#endif
#if SOA_HAS_EXT
    public SOA_EXT_TYPE ext;
#endif

    public SOA_TUPLE_NAME toSOA() { return this; }
    public static SOA_TUPLE_NAME fromSOA(SOA_TUPLE_NAME soa) { return soa; }

    [ForceInline]
    public SOA_WORD_TYPE getWord(uint idx)
    {
#if SOA_HAS_UNITS && SOA_HAS_EXT
        if (idx < (SOA_UNITS_PER_ELEM * SOA_WORDS_PER_UNIT))
            return units[int(idx / SOA_WORDS_PER_UNIT)][int(idx % SOA_WORDS_PER_UNIT)];
        else
            return ext[int(idx % SOA_WORDS_PER_UNIT)];
#elif SOA_HAS_UNITS
        return units[int(idx / SOA_WORDS_PER_UNIT)][int(idx % SOA_WORDS_PER_UNIT)];
#elif SOA_HAS_EXT
        return ext[int(idx)];
#endif
    }
    [ForceInline]
    public vector<SOA_WORD_TYPE, Count_V> getWords<let Count_V : uint>(uint idx)
    {
        vector<SOA_WORD_TYPE, Count_V> words;
        [ForceUnroll]
        for (uint i = 0; i < Count_V; ++i)
            words[i] = getWord(idx + i);
        return words;
    }
    [ForceInline]
    [mutating]
    public void setWord(uint idx, SOA_WORD_TYPE word)
    {
#if SOA_HAS_UNITS && SOA_HAS_EXT
        if (idx < (SOA_UNITS_PER_ELEM * SOA_WORDS_PER_UNIT))
            units[int(idx / SOA_WORDS_PER_UNIT)][int(idx % SOA_WORDS_PER_UNIT)] = word;
        else
            ext[int(idx % SOA_WORDS_PER_UNIT)] = word;
#elif SOA_HAS_UNITS
        units[int(idx / SOA_WORDS_PER_UNIT)][int(idx % SOA_WORDS_PER_UNIT)] = word;
#elif SOA_HAS_EXT
        ext[int(idx)] = word;
#endif
    }
    [ForceInline]
    [mutating]
    public void setWords<let Count_V : uint>(uint idx, vector<SOA_WORD_TYPE, Count_V> words)
    {
        [ForceUnroll]
        for (uint i = 0; i < Count_V; ++i)
            setWord(idx + i, words[i]);
    }
}

#ifdef SOA_BUFFER_NAME
public struct SOA_BUFFER_NAME
{
#if SOA_HAS_UNITS
    public StructuredBuffer<SOA_UNIT_TYPE> unitBufs[SOA_UNITS_PER_ELEM];
#endif

#if SOA_HAS_EXT
    public StructuredBuffer<SOA_PADDED_EXT_TYPE> extBuf;
#endif

    public T load<T : SOA_IELEMENT_NAME>(uint id)
    {
        SOA_TUPLE_NAME a;

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            a.units[i] = unitBufs[i][id];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        a.ext = extBuf[id].xyz;
#else
        a.ext = extBuf[id];
#endif
#endif
        return T::fromSOA(a);
    }
}
#endif // SOA_BUFFER_NAME

#ifdef SOA_RWBUFFER_NAME
public struct SOA_RWBUFFER_NAME
{
#if SOA_HAS_UNITS
    public RWStructuredBuffer<SOA_UNIT_TYPE> unitBufs[SOA_UNITS_PER_ELEM];
#endif

#if SOA_HAS_EXT
    public RWStructuredBuffer<SOA_PADDED_EXT_TYPE> extBuf;
#endif

    public T load<T : SOA_IELEMENT_NAME>(uint id)
    {
        SOA_TUPLE_NAME a;

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            a.units[i] = unitBufs[i][id];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        a.ext = extBuf[id].xyz;
#else
        a.ext = extBuf[id];
#endif
#endif
        return T::fromSOA(a);
    }

    public void store<T : SOA_IELEMENT_NAME>(uint id, T elem)
    {
        SOA_TUPLE_NAME a = elem.toSOA();

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            unitBufs[i][id] = a.units[i];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        extBuf[id] = SOA_PADDED_EXT_TYPE(a.ext, 0);
#else
        extBuf[id] = a.ext;
#endif
#endif
    }
}
#endif // SOA_RWBUFFER_NAME

#ifdef SOA_TEXTURE2D_NAME
public struct SOA_TEXTURE2D_NAME
{
#if SOA_HAS_UNITS
    public Texture2D<SOA_UNIT_TYPE> unitTexs[SOA_UNITS_PER_ELEM];
#endif

#if SOA_HAS_EXT
    public Texture2D<SOA_TEXTURE_PADDED_EXT_TYPE> extTex;
#endif

    public T load<T : SOA_IELEMENT_NAME>(uint2 coord)
    {
        SOA_TUPLE_NAME a;

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            a.units[i] = unitTexs[i][coord];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        a.ext = extTex[coord].xyz;
#else
        a.ext = extTex[coord];
#endif
#endif
        return T::fromSOA(a);
    }
}
#endif // SOA_TEXTURE2D_NAME

#ifdef SOA_RWTEXTURE2D_NAME
public struct SOA_RWTEXTURE2D_NAME
{
#if SOA_HAS_UNITS
    public RWTexture2D<SOA_UNIT_TYPE> unitTexs[SOA_UNITS_PER_ELEM];
#endif

#if SOA_HAS_EXT
    public RWTexture2D<SOA_TEXTURE_PADDED_EXT_TYPE> extTex;
#endif

    public T load<T : SOA_IELEMENT_NAME>(uint2 coord)
    {
        SOA_TUPLE_NAME a;

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            a.units[i] = unitTexs[i][coord];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        a.ext = extTex[coord].xyz;
#else
        a.ext = extTex[coord];
#endif
#endif
        return T::fromSOA(a);
    }

    public void store<T : SOA_IELEMENT_NAME>(uint2 coord, T elem)
    {
        SOA_TUPLE_NAME a = elem.toSOA();

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            unitTexs[i][coord] = a.units[i];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        extTex[coord] = SOA_PADDED_EXT_TYPE(a.ext, 0);
#else
        extTex[coord] = a.ext;
#endif
#endif
    }
}
#endif // SOA_RWTEXTURE2D_NAME

#ifdef SOA_COHERENTTEXTURE2D_NAME
public struct SOA_COHERENTTEXTURE2D_NAME
{
#if SOA_HAS_UNITS
    public globallycoherent RWTexture2D<SOA_UNIT_TYPE> unitTexs[SOA_UNITS_PER_ELEM];
#endif

#if SOA_HAS_EXT
    public globallycoherent RWTexture2D<SOA_TEXTURE_PADDED_EXT_TYPE> extTex;
#endif

    public T load<T : SOA_IELEMENT_NAME>(uint2 coord)
    {
        SOA_TUPLE_NAME a;

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            a.units[i] = unitTexs[i][coord];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        a.ext = extTex[coord].xyz;
#else
        a.ext = extTex[coord];
#endif
#endif
        return T::fromSOA(a);
    }

    public void store<T : SOA_IELEMENT_NAME>(uint2 coord, T elem)
    {
        SOA_TUPLE_NAME a = elem.toSOA();

#if SOA_HAS_UNITS
        [ForceUnroll]
        for (uint i = 0; i < SOA_UNITS_PER_ELEM; ++i)
            unitTexs[i][coord] = a.units[i];
#endif

#if SOA_HAS_EXT
#if SOA_IS_EXT_PADDED
        extTex[coord] = SOA_PADDED_EXT_TYPE(a.ext, 0);
#else
        extTex[coord] = a.ext;
#endif
#endif
    }
}
#endif // SOA_COHERENTTEXTURE2D_NAME

#endif

#ifdef SOA_BUFFER_UNDEF

#if SOA_HAS_UNITS
#undef SOA_UNIT_TYPE
#endif

#if SOA_HAS_EXT
#undef SOA_IS_EXT_PADDED
#undef SOA_PADDED_WORDS_PER_EXT
#undef SOA_EXT_TYPE
#undef SOA_PADDED_EXT_TYPE
#undef SOA_TEXTURE_PADDED_EXT_TYPE
#endif

#undef SOA_HAS_UNITS
#undef SOA_HAS_EXT

#undef SOA_UNITS_PER_ELEM
#undef SOA_WORDS_PER_EXT

#endif
